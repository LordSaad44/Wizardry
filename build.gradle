buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'http://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = 'com.teamwizardry.wizardry'
archivesBaseName = 'wizardry'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: mcp_mappings_channel, version: mcp_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            arg "--mixin.config=" + archivesBaseName + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'

            mods {
                wizardry {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg "--mixin.config=" + archivesBaseName + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                wizardry {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'wizardry', '--all', '--output', file('src/generated/resources/')

            mods {
                wizardry {
                    source sourceSets.main
                }
            }
        }
    }
}

allprojects {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/teamwizardry/teamwizardry' }
        maven { url 'https://minecraft.curseforge.com/api/maven/' }
        maven { url 'http://repo.spongepowered.org/maven' }
        maven { url 'http://maven.shadowfacts.net/' }
        maven {
            // for JEI
            name "progwml6"
            url "http://dvs1.progwml6.com/files/maven"
        }
        maven {
            name "curios"
            url "https://maven.theillusivec4.top/"
        }
        maven { url = 'http://repo.spongepowered.org/maven' }

        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${mc_version}-${forge_version}"

    implementation 'org.yaml:snakeyaml:1.21'
    [
            "com.teamwizardry.librarianlib:librarianlib-prism:$liblib_version",
            "com.teamwizardry.librarianlib:librarianlib-facade:$liblib_version",
            "com.teamwizardry.librarianlib:librarianlib-glitter:$liblib_version",
            "com.teamwizardry.librarianlib:librarianlib-foundation:$liblib_version",
            "com.teamwizardry.librarianlib:librarianlib-etcetera:$liblib_version"
    ].each {
        implementation(it) { exclude group: 'kottle', module: 'Kottle' }
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'

    compileOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-1.15.2-2.0:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-1.15.2-2.0")

    compile 'org.spongepowered:spongeapi:7.0.0'
    compile 'org.spongepowered:mixin:0.7.5-SNAPSHOT'

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // replace version
        expand 'version': project.version
    }

    // copy everything else, thats not the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {
    archiveName = "${mod_id}-${version}.${extension}"

    manifest {
        attributes([
                "MixinConfigs"            : "${mod_id}.mixins.json",
                "Specification-Title"     : "${mod_id}",
                "Specification-Vendor"    : "${mod_authors}",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "${mod_id}",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${mod_authors}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

test {
    useJUnitPlatform()
    testLogging.exceptionFormat = 'full'
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        html.destination file("$projectDir/jacoco")
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}